sourceSets {
    funcTest {
        compileClasspath += sourceSets.main.output + sourceSets.main.runtimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

task funcTest(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs the functional tests.'

    testClassesDir = sourceSets.funcTest.output.classesDir
    classpath = sourceSets.funcTest.runtimeClasspath
}

gradlePlugin {
    testSourceSets sourceSets.funcTest
}

check.dependsOn funcTest

gradle.projectsEvaluated {
    def quickTasks = []

    gradle.rootProject.allprojects.each { project ->
        quickTasks.addAll(project.tasks.findAll { it.name == 'test' || it.name == 'integTest' })
        quickTasks.addAll(project.tasks.withType(FindBugs))
        quickTasks.addAll(project.tasks.withType(Pmd))
        quickTasks.addAll(project.tasks.withType(CodeNarc))
    }

    quickTasks.each { task ->
        project.tasks.funcTask.shouldRunAfter task
    }
}

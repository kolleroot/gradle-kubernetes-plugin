import com.github.kolleroot.gradle.kubernetes.buildsrc.MutateTopLevelApiElement
import io.swagger.codegen.config.CodegenConfigurator
import org.detoeuf.SwaggerCodeGenTask

plugins {
    id 'java'
    id 'groovy'
    id 'codenarc'
}

repositories {
    jcenter()
}

sourceSets {
    main {
        java {
            srcDirs += [
                    "$buildDir/swagger/kubernetes/model"
            ]
        }
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile "io.swagger:swagger-annotations:${libraryVersions.swaggerAnnotations}"
    compile "com.owlike:genson:${libraryVersions.genson}"
    compile 'com.google.code.gson:gson:2.8.0'

    testCompile project(':test-utils')
}

task generateKubernetesModel(type: SwaggerCodeGenTask) {
    configurator = new CodegenConfigurator()
            .setLang('java')
            .setOutputDir("$buildDir/swagger/kubernetes/codegen")
            .setInputSpec("$projectDir/src/main/swagger/swagger.json")
    //.setInputSpec('https://raw.githubusercontent.com/kubernetes/kubernetes/master/api/openapi-spec/swagger
    //        .json')
            .setTemplateDir("$projectDir/src/main/swagger/tpl")
            .addAdditionalProperty('dateLibrary', 'java8')
    //.addTypeMapping('array', 'org.gradle.model.ModelSet')
    //.addTypeMapping('map', 'org.gradle.model.ModelMap')
            .addTypeMapping('resource.Quantity', 'String')
            .addTypeMapping('intstr.IntOrString', 'String')
            .addTypeMapping('byte[]', 'String')
            .addLanguageSpecificPrimitive('String')
            .setModelPackage('com.github.kolleroot.gradle.kubernetes.model.api')
}

task extractGeneratedKubernetesModel(type: Copy, dependsOn: generateKubernetesModel) {
    from("$buildDir/swagger/kubernetes/codegen/src/main/java") {
        include 'com/github/kolleroot/gradle/kubernetes/model/api/*.java'
    }

    into "$buildDir/swagger/kubernetes/extracted"
}

task fixTopLevelObjects(type: MutateTopLevelApiElement, dependsOn: extractGeneratedKubernetesModel) {
    source = extractGeneratedKubernetesModel

    ignoreClasses << 'V1OwnerReference.java'

    destinationDir = "$buildDir/swagger/kubernetes/model"
}

compileJava.dependsOn fixTopLevelObjects

codenarcMain {
    configFile file("${rootDir}/config/codenarc/codenarc-main.rules")
}

codenarcTest {
    configFile file("${rootDir}/config/codenarc/codenarc-test.rules")
}

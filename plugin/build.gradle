plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'codenarc'
}

apply from: "${project.rootDir}/gradle/testBase.gradle"
apply from: "${project.rootDir}/gradle/integTest.gradle"

group = 'com.github.kolleroot.gradle'
version = '0.1'

dependencies {
    compile localGroovy()
    compile gradleApi()

    compile 'com.bmuschko:gradle-docker-plugin:3.0.3'
    compile "io.fabric8:kubernetes-client:${libraryVersions.kubernetesClient}"

    testBaseCompile gradleTestKit()
    testBaseCompile "junit:junit:${libraryVersions.junit}"
    testBaseCompile("org.spockframework:spock-core:${libraryVersions.spock}") {
        exclude group: 'org.codehaus.groovy'
    }
    testBaseCompile 'org.apache.commons:commons-lang3:3.5'

    testBaseCompile 'com.github.docker-java:docker-java:3.0.6'
}

gradlePlugin {
    plugins {
        kubernetesDeploymentPlugin {
            id = 'com.github.kolleroot.gradle.kubernetes'
            implementationClass = 'com.github.kolleroot.gradle.kubernetes.KubernetesPlugin'
        }
    }
}

codenarcMain {
    configFile file("${rootDir}/config/codenarc/codenarc-main.rules")
}

codenarcTestBase {
    configFile file("${rootDir}/config/codenarc/codenarc-test.rules")
}

codenarcTest {
    configFile file("${rootDir}/config/codenarc/codenarc-test.rules")
}

codenarcIntegTest {
    configFile file("${rootDir}/config/codenarc/codenarc-test.rules")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.github.kolleroot.gradle'
            artifactId 'gradle-kubernetes-plugin'
            version '0.1'

            from components.java
        }
    }
}
